############################################################################################
#                                        Structure                                         #
############################################################################################

"""
    CoDifferential{manifold_dim, form_rank, expression_rank, G, F} <:
    AbstractForm{manifold_dim, form_rank, expression_rank, G}

Represents the codifferential of an `AbstractForm`.

# Fields
- `form::AbstractForm{manifold_dim, form_rank, expression_rank, G}`: The form to
    which the codifferential is applied.
- `label::String`: The codifferential label. This is a concatenation of `"d*"` with the
    label of `form`.

# Type parameters
- `manifold_dim`: Dimension of the manifold.
- `form_rank`: The form rank of the codifferential. If the form rank of `form` is `k`
    then `form_rank` is `k-1`.
- `expression_rank`: Rank of the expression. Expressions without basis forms have rank 0,
    with one single set of basis forms have rank 1, with two sets of basis forms have rank
    2. Higher ranks are not possible.
- `G <: Geometry.AbstractGeometry{manifold_dim}`: Type of the underlying geometry.
- `F <: Forms.AbstractForm{manifold_dim, form_rank+1, expression_rank, G}`: The
    type of `form`.

# Inner Constructors
- `CoDifferential(form::F)`: General constructor.
"""
struct CoDifferential{manifold_dim, form_rank, expression_rank, G, F} <:
       AbstractForm{manifold_dim, form_rank, expression_rank, G}
    form::F
    label::String

    function CoDifferential(
        form::F
    ) where {
        manifold_dim,
        form_rank,
        expression_rank,
        G <: Geometry.AbstractGeometry{manifold_dim},
        F <: AbstractForm{manifold_dim, form_rank, expression_rank, G},
    }
        if form_rank == 0
            throw(ArgumentError("""\
                Tried to compute the codifferential of a zero form. The manifold \
                dimension is $(manifold_dim) and the form rank is $(form_rank). \
                """))
        elseif form_rank > 1
            throw(
                ArgumentError("""\
              Tried to compute the codifferential of a form with form_rank > 1. This has \
              not been implemented yet. \
              """)
            )
        end

        return new{manifold_dim, form_rank - 1, expression_rank, G, F}(
            form, "δ(" * get_label(form) * ")"
        )
    end
end

const codifferential = CoDifferential
const dstar = CoDifferential
const δ = CoDifferential

get_form(co_der::CoDifferential) = co_der.form
get_geometry(co_der::CoDifferential) = get_geometry(get_form(co_der))

function evaluate(
    form::CoDifferential{manifold_dim},
    element_id::Int,
    xi::Points.AbstractPoints{manifold_dim},
) where {manifold_dim}
    return _evaluate_codifferential(get_form(form), element_id, xi)
end

function _evaluate_codifferential(
    form::AbstractForm{manifold_dim}, ::Int, ::Points.AbstractPoints{manifold_dim}
) where {manifold_dim}
    throw(ArgumentError("Method not implement for type $(typeof(form))."))
end

############################################################################################
#                                        Form Field                                        #
############################################################################################

function _evaluate_codifferential(
    form::AbstractFormField{manifold_dim, form_rank, G},
    element_id::Int,
    xi::Points.AbstractPoints{manifold_dim},
) where {manifold_dim, form_rank, G <: Geometry.AbstractGeometry{manifold_dim}}
    d_form_basis_eval, form_basis_indices = _evaluate_codifferential(
        get_form(form), element_id, xi
    )

    # This is equal to binomial(manifold_dim, form_rank + 1).
    n_derivative_components = size(d_form_basis_eval, 1)

    d_form_eval = Vector{Vector{Float64}}(undef, n_derivative_components)

    for derivative_form_component_idx in 1:n_derivative_components
        d_form_eval[derivative_form_component_idx] =
            d_form_basis_eval[derivative_form_component_idx] *
            form.coefficients[form_basis_indices[1]]
    end

    # We need to wrap form_basis_indices in [] to return a vector of vector to allow
    # multi-indexed expressions, like wedges.
    return d_form_eval, [[1]]
end

function _evaluate_codifferential(
    form::ExteriorDerivative{2, 1, 0}, element_id::Int, xi::Points.AbstractPoints{2}
)
    field = get_form(form)
    space = get_form(field)
    d_form_basis_eval, form_basis_indices = _evaluate_codifferential(
        d(space), element_id, xi
    )

    # This is equal to binomial(manifold_dim, form_rank + 1).
    n_derivative_components = size(d_form_basis_eval, 1)

    d_form_eval = Vector{Vector{Float64}}(undef, n_derivative_components)

    for derivative_form_component_idx in 1:n_derivative_components
        d_form_eval[derivative_form_component_idx] =
            d_form_basis_eval[derivative_form_component_idx] *
            field.coefficients[form_basis_indices[1]]
    end

    # We need to wrap form_basis_indices in [] to return a vector of vector to allow
    # multi-indexed expressions, like wedges.
    return d_form_eval, [[1]]
end

############################################################################################
#                                        Form Space                                        #
############################################################################################

# 1D 1-forms.
function _evaluate_codifferential(
    form_space::FS, element_id::Int, xi::Points.AbstractPoints{1}
) where {G <: Geometry.AbstractGeometry{1}, FS <: FormSpace{1, 1, G}}
    manifold_dim = 1
    n_coderivative_form_components = 1
    n_basis_functions = Forms.get_num_basis(form_space, element_id)
    n_evaluation_points = Points.get_num_points(xi)

    # Preallocate memory for output array
    codiff_eval = [
        zeros(Float64, n_evaluation_points, n_basis_functions) for
        _ in 1:n_coderivative_form_components
    ]

    # Evaluate derivatives of the basis functions. We need derivatives up to order 1.
    fem_evals, form_basis_indices = _evaluate_form_in_canonical_coordinates(
        form_space, element_id, xi, 1
    )

    # Compute the metric terms, including derivative of the metric.
    J, inv_g, g, sqrt_g, dgdu, dinv_g_du, dsqrt_g_du, Hs = Geometry.metric_derivatives(
        get_geometry(form_space), element_id, xi
    )

    # Compute the codifferential.
    # α^1 = α¹ du
    # *d*α¹ = (1/sqrt(det(g))) * (α¹ d/dx(1/sqrt(det(g))) + (1/sqrt(det(g)))d/dx(α¹))
    idx_du = FunctionSpaces.get_derivative_idx([1])
    for i in 1:n_evaluation_points
        codiff_eval[1][i, :] .=
            (1.0 / sqrt_g[i]) .* (
                .+fem_evals[1][1][1][i, :] .* dsqrt_g_du[i]  # α¹ * ∂u sqrt(g)
                .+
                fem_evals[2][idx_du][1][i, :] .* (1.0 / sqrt_g[i])  # ∂u α¹ * 1 / sqrt(g)
            )
    end

    return codiff_eval, form_basis_indices
end

# 1D 1-forms where the 1-form is the exterior derivative of a 0-form.
function _evaluate_codifferential(
    form_space::F, element_id::Int, xi::Points.AbstractPoints{1}
) where {
    G <: Geometry.AbstractGeometry{1},
    FS <: FormSpace{1, 0, G},
    F <: ExteriorDerivative{1, 1, 1, G, FS},
}
    manifold_dim = 1
    n_coderivative_form_components = 1
    n_basis_functions = Forms.get_num_basis(form_space, element_id)
    n_evaluation_points = Points.get_num_points(xi)

    # Preallocate memory for output array
    codiff_eval = [
        zeros(Float64, n_evaluation_points, n_basis_functions) for
        _ in 1:n_coderivative_form_components
    ]

    # Evaluate derivatives of the basis functions. We need derivatives up to order 2. Since
    # we are evaluating the laplacian of 0-forms, we do not have to scale the derivatives.
    fem_evals, form_basis_indices = FunctionSpaces.evaluate(
        get_fe_space(form_space), element_id, xi, 2
    )

    # Compute the metric terms, including derivative of the metric.
    J, inv_g, g, sqrt_g, (dgdu,), (dinv_g_du,), (dsqrt_g_du,), Hs = Geometry.metric_derivatives(
        get_geometry(form_space), element_id, xi
    )

    # Compute the laplacian.
    # α^1 = α¹ du
    # α¹ = d(β⁰) = ∂ᵤ β⁰ du
    # *d*α¹ = (1/sqrt(det(g))) * (α¹ d/dx(1/sqrt(det(g))) + (1/sqrt(det(g)))d/dx(α¹))
    idx_du = FunctionSpaces.get_derivative_idx([1])
    idx_duu = FunctionSpaces.get_derivative_idx([2])
    for i in 1:n_evaluation_points
        codiff_eval[1][i, :] .=
            (1.0 / sqrt_g[i]) .* (
                .+fem_evals[2][idx_du][1][i, :] .* dsqrt_g_du[i]  # α¹ * ∂u sqrt(g)
                .+
                fem_evals[3][idx_duu][1][i, :] .* (1.0 / sqrt_g[i])  # ∂u α¹ * 1 / sqrt(g)
            )
    end

    return codiff_eval, [form_basis_indices]
end

# 2D 1-forms.
function _evaluate_codifferential(
    form_space::FS, element_id::Int, xi::Points.AbstractPoints{2}
) where {G <: Geometry.AbstractGeometry{2}, FS <: FormSpace{2, 1, G}}
    # if typeof(form_space) != FunctionSpaces.RaviartThomas{2, 1, G}
    #     throw(ArgumentError("""\
    #         Currently only implemented for 2D 1-forms with Raviart-Thomas basis functions. \
    #         The provided form space is of type $(typeof(form_space)). \
    #         """))
    # end

    manifold_dim = 2
    n_coderivative_form_components = 1
    n_basis_functions = Forms.get_num_basis(form_space, element_id)
    n_evaluation_points = Points.get_num_points(xi)

    # Preallocate memory for output array
    codiff_eval = [
        zeros(Float64, n_evaluation_points, n_basis_functions) for
        _ in 1:n_coderivative_form_components
    ]

    # Evaluate derivatives of the basis functions. We need derivatives up to order 1.
    fem_evals, form_basis_indices = _evaluate_form_in_canonical_coordinates(
        form_space, element_id, xi, 1
    )

    # Compute the metric terms, including derivative of the metric.
    J, inv_g, g, sqrt_g, (dgdu, dgdv), (dinv_g_du, dinv_g_dv), (dsqrt_g_du, dsqrt_g_dv), Hs = Geometry.metric_derivatives(
        get_geometry(form_space), element_id, xi
    )

    # Compute the coderivative.
    # α^1 = α¹ du + α² dv
    # d*α¹ = β⁰ =
    idx_du = FunctionSpaces.get_derivative_idx([1, 0])
    idx_dv = FunctionSpaces.get_derivative_idx([0, 1])
    for i in 1:n_evaluation_points
        codiff_eval[1][i, :] .=
            fem_evals[2][idx_du][1][i, :] .* inv_g[i][1, 1] .+  # ∂u α¹ * g¹¹
            fem_evals[1][1][1][i, :] .* dinv_g_du[i][1, 1] .+  # α¹ * ∂u g¹¹
            fem_evals[2][idx_du][2][i, :] .* inv_g[i][1, 2] .+  # ∂u α² * g¹²
            fem_evals[1][1][2][i, :] .* dinv_g_du[i][1, 2] .+  # α² * ∂u g¹²
            fem_evals[2][idx_dv][1][i, :] .* inv_g[i][2, 1] .+  # ∂v α¹ * g²¹
            fem_evals[1][1][1][i, :] .* dinv_g_dv[i][2, 1] .+  # α¹ * ∂v g²¹
            fem_evals[2][idx_dv][2][i, :] .* inv_g[i][2, 2] .+  # ∂v α² * g²²
            fem_evals[1][1][2][i, :] .* dinv_g_dv[i][2, 2] .+  # α² * ∂v g²²
            (1.0 / sqrt_g[i]) .* (
                .+fem_evals[1][1][1][i, :] .* inv_g[i][1, 1] .* dsqrt_g_du[i]  # α¹ * g¹¹ * ∂u sqrt(g)
                .+
                fem_evals[1][1][2][i, :] .* inv_g[i][1, 2] .* dsqrt_g_du[i]  # α² * g¹² * ∂u sqrt(g)
                .+
                fem_evals[1][1][1][i, :] .* inv_g[i][2, 1] .* dsqrt_g_dv[i]  # α¹ * g²¹ * ∂v sqrt(g)
                .+
                fem_evals[1][1][2][i, :] .* inv_g[i][2, 2] .* dsqrt_g_dv[i]  # α² * g²² * ∂v sqrt(g)
            )
    end

    return codiff_eval, form_basis_indices
end

# Specialised version for the exterior derivative of 0-forms to 1-forms in 2D.
# This is equivalent to the Laplacian of 0-forms.
function _evaluate_codifferential(
    form_space::F, element_id::Int, xi::Points.AbstractPoints{2}
) where {
    G <: Geometry.AbstractGeometry{2},
    FS <: FormSpace{2, 0, G},
    F <: ExteriorDerivative{2, 1, 1, G, FS},
}
    manifold_dim = 2
    n_coderivative_form_components = 1
    n_basis_functions = Forms.get_num_basis(form_space, element_id)
    n_evaluation_points = Points.get_num_points(xi)

    # Preallocate memory for output array
    codiff_eval = [
        zeros(Float64, n_evaluation_points, n_basis_functions) for
        _ in 1:n_coderivative_form_components
    ]

    # Evaluate derivatives of the basis functions. We need derivatives up to order 2. Since
    # we are evaluating the laplacian of 0-forms, the basis functions we do not have to
    # scale the derivatives.
    fem_evals, form_basis_indices = FunctionSpaces.evaluate(
        get_fe_space(form_space), element_id, xi, 2
    )

    # # Compute the metric terms, including derivative of the metric.
    J, inv_g, g, sqrt_g, (dgdu, dgdv), (dinv_g_du, dinv_g_dv), (dsqrt_g_du, dsqrt_g_dv), Hs = Geometry.metric_derivatives(
        get_geometry(form_space), element_id, xi
    )

    # Compute the laplacian, which is the coderivative of the exterior derivative.
    # α^1 = α¹ du + α² dv
    # α¹ = d(β⁰) = ∂ᵤ β⁰ du + ∂ᵥ β⁰ dv
    idx_du = FunctionSpaces.get_derivative_idx([1, 0])
    idx_dv = FunctionSpaces.get_derivative_idx([0, 1])
    idx_duu = FunctionSpaces.get_derivative_idx([2, 0])
    idx_dvv = FunctionSpaces.get_derivative_idx([0, 2])
    idx_duv = FunctionSpaces.get_derivative_idx([1, 1])

    for i in 1:n_evaluation_points
        codiff_eval[1][i, :] .=
            fem_evals[3][idx_duu][1][i, :] .* inv_g[i][1, 1] .+  # ∂u α¹ * g¹¹
            fem_evals[2][idx_du][1][i, :] .* dinv_g_du[i][1, 1] .+  # α¹ * ∂u g¹¹
            fem_evals[3][idx_duv][1][i, :] .* inv_g[i][1, 2] .+  # ∂u α² * g¹²
            fem_evals[2][idx_dv][1][i, :] .* dinv_g_du[i][1, 2] .+  # α² * ∂u g¹²
            fem_evals[3][idx_duv][1][i, :] .* inv_g[i][2, 1] .+  # ∂v α¹ * g²¹
            fem_evals[2][idx_du][1][i, :] .* dinv_g_dv[i][2, 1] .+  # α¹ * ∂v g²¹
            fem_evals[3][idx_dvv][1][i, :] .* inv_g[i][2, 2] .+  # ∂v α² * g²²
            fem_evals[2][idx_dv][1][i, :] .* dinv_g_dv[i][2, 2] .+  # α² * ∂v g²²
            (1.0 / sqrt_g[i]) .* (
                fem_evals[2][idx_du][1][i, :] .* inv_g[i][1, 1] .* dsqrt_g_du[i] .+  # α¹ * g¹¹ * ∂u sqrt(g)
                fem_evals[2][idx_dv][1][i, :] .* inv_g[i][1, 2] .* dsqrt_g_du[i] .+  # α² * g¹² * ∂u sqrt(g)
                fem_evals[2][idx_du][1][i, :] .* inv_g[i][2, 1] .* dsqrt_g_dv[i] .+  # α¹ * g²¹ * ∂v sqrt(g)
                fem_evals[2][idx_dv][1][i, :] .* inv_g[i][2, 2] .* dsqrt_g_dv[i]  # α² * g²² * ∂v sqrt(g)
            )
    end

    return codiff_eval, [form_basis_indices]
end

# Currently only implemented for 2D 1-forms.
function _evaluate_codifferential(
    form_space::FS, element_id::Int, xi::Points.AbstractPoints{2}
) where {F, G <: Geometry.AbstractGeometry{2}, FS <: ModifiedOneFormSpace{2, G, F}}
    manifold_dim = 2
    n_coderivative_form_components = 1
    n_basis_functions = Forms.get_num_basis(form_space, element_id)
    n_evaluation_points = Points.get_num_points(xi)

    # Preallocate memory for output array
    codiff_eval = [
        zeros(Float64, n_evaluation_points, n_basis_functions) for
        _ in 1:n_coderivative_form_components
    ]

    # Compute the metric terms, including derivative of the metric.
    J, inv_g, g, sqrt_g, (dgdu, dgdv), (dinv_g_du, dinv_g_dv), (dsqrt_g_du, dsqrt_g_dv), Hs = Geometry.metric_derivatives(
        get_geometry(form_space), element_id, xi
    )

    # Evaluate derivatives of the basis functions. We need derivatives up to order 1.
    fem_evals, form_basis_indices = _evaluate_form_in_canonical_coordinates(
        form_space, element_id, xi, 1
    )
    # Evaluate the form again, after which we can scale by the hessian entries.
    local_form_basis, form_basis_indices2 = FunctionSpaces.evaluate(
        get_fe_space(form_space), element_id, xi, 0
    )
    local_form_basis_xixi = [
        local_form_basis[1][1][1][p, :] .* Hs[p][1][1, 1] .+
        local_form_basis[1][1][2][p, :] .* Hs[p][2][1, 1] for p in 1:n_evaluation_points
    ]
    local_form_basis_xieta = [
        local_form_basis[1][1][1][p, :] .* Hs[p][1][2, 1] .+
        local_form_basis[1][1][2][p, :] .* Hs[p][2][2, 1] for p in 1:n_evaluation_points
    ]
    local_form_basis_etaxi = [
        local_form_basis[1][1][1][p, :] .* Hs[p][1][1, 2] .+
        local_form_basis[1][1][2][p, :] .* Hs[p][2][1, 2] for p in 1:n_evaluation_points
    ]
    local_form_basis_etaeta = [
        local_form_basis[1][1][1][p, :] .* Hs[p][1][2, 2] .+
        local_form_basis[1][1][2][p, :] .* Hs[p][2][2, 2] for p in 1:n_evaluation_points
    ]
    # println("New stuff")
    # Compute the coderivative.
    # α^1 = α¹ du + α² dv
    # d*α¹ = β⁰ =
    idx_du = FunctionSpaces.get_derivative_idx([1, 0])
    idx_dv = FunctionSpaces.get_derivative_idx([0, 1])
    for i in 1:n_evaluation_points
        # Compute the terms involving the derivatives of the basis functions
        fem_evals_du_invg11 =
            fem_evals[2][idx_du][1][i, :] .* inv_g[i][1, 1] .+
            local_form_basis_xixi[i] .* inv_g[i][1, 1]
        fem_evals_du_invg12 =
            fem_evals[2][idx_du][2][i, :] .* inv_g[i][1, 2] .+
            local_form_basis_etaxi[i] .* inv_g[i][1, 2]
        fem_evals_dv_invg21 =
            fem_evals[2][idx_dv][1][i, :] .* inv_g[i][2, 1] .+
            local_form_basis_xieta[i] .* inv_g[i][2, 1]
        fem_evals_dv_invg22 =
            fem_evals[2][idx_dv][2][i, :] .* inv_g[i][2, 2] .+
            local_form_basis_etaeta[i] .* inv_g[i][2, 2]

        # Now add the terms involving the basis functions scaled by the metric derivatives
        # and the terms involving the basis functions scaled by the derivatives of sqrt(g)
        codiff_eval[1][i, :] .=
            fem_evals_du_invg11 .+  # ∂u α¹ * g¹¹
            fem_evals[1][1][1][i, :] .* dinv_g_du[i][1, 1] .+  # α¹ * ∂u g¹¹
            fem_evals_du_invg12 .+  # ∂u α² * g¹²
            fem_evals[1][1][2][i, :] .* dinv_g_du[i][1, 2] .+  # α² * ∂u g¹²
            fem_evals_dv_invg21 .+  # ∂v α¹ * g²¹
            fem_evals[1][1][1][i, :] .* dinv_g_dv[i][2, 1] .+  # α¹ * ∂v g²¹
            fem_evals_dv_invg22 .+  # ∂v α² * g²²
            fem_evals[1][1][2][i, :] .* dinv_g_dv[i][2, 2] .+  # α² * ∂v g²²
            (1.0 / sqrt_g[i]) .* (
                .+fem_evals[1][1][1][i, :] .* inv_g[i][1, 1] .* dsqrt_g_du[i]  # α¹ * g¹¹ * ∂u sqrt(g)
                .+
                fem_evals[1][1][2][i, :] .* inv_g[i][1, 2] .* dsqrt_g_du[i]  # α² * g¹² * ∂u sqrt(g)
                .+
                fem_evals[1][1][1][i, :] .* inv_g[i][2, 1] .* dsqrt_g_dv[i]  # α¹ * g²¹ * ∂v sqrt(g)
                .+
                fem_evals[1][1][2][i, :] .* inv_g[i][2, 2] .* dsqrt_g_dv[i]  # α² * g²² * ∂v sqrt(g)
            )
    end

    return codiff_eval, form_basis_indices
end
